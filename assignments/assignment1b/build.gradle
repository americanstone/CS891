// Top-level build file where you can add configuration
// options common to all sub-projects/modules.
buildscript {
    ext {
        versionMajor = 1
        versionMinor = 0
        versionPatch = 2

        versions = [
                android          : '3.5.0',
                kotlin           : "1.3.50",
                coroutines       : '1.3.0',
                anko             : "0.10.5",
                support          : '1.0.0',
                annotation       : '1.1.0',
                legacy_support   : '1.0.0',
                appcompat        : "1.0.2",
                material         : "1.0.0",
                constraint_layout: "1.1.3",
                arch_components  : "2.0.0",
                core_ktx         : "1.0.2",
                viewmodel_ktx    : "2.0.0",
                glide            : '4.9.0',
                picasso          : "2.71828",
                gson             : "2.8.5",
                annotations      : "13.0",
                commons_io       : '20030203.000550',
                jsoup            : '1.12.1',
                leak_canary      : "1.6.1",
                rxjava           : "2.2.6",
                rxandroid        : "2.1.1",
                rxbinding        : "2.2.0",
                espresso         : '3.2.0',
                okhttp           : '4.9.0',
                junit            : "4.12",
                mockito          : "2.19.0"
        ]

        build_versions = [
                compileSdk: 28,
                targetSdk : 28,
                minSdk    : 26,
                java      : 1.8,
                code      : versionMajor * 10000 + versionMinor * 100 + versionPatch,
                name      : "${versionMajor}.${versionMinor}.${versionPatch}"
        ]
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$versions.android"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
    
		// Required by Vanderbilt autograder - do not remove
		classpath 'edu.vanderbilt.grader:gradle-plugin:1.4.64'
	}
}

plugins {
    // Specifying 'apply false' eliminates gradle warning message:
    // "Kotlin plugin should be enabled before 'kotlin-kapt'"
    id "org.jetbrains.kotlin.kapt" version "1.3.41" apply false
}

// Block that initializes settings common to all project modules (sub-projects).
allprojects {
    repositories {
        google()
        jcenter()
    }

    // This is an example of how to ensure that a project with multiple android
    // sub-modules will use the same android plugin settings and versions. This
    // project only has a single Android module so this is not really necessary
    // here, but it also doesn't hurt.
    subprojects {
        afterEvaluate {
            if (project.plugins.findPlugin('android') ?: project.plugins.findPlugin('android-library')) {
                android {
                    compileSdkVersion build_versions.compileSdk

                    defaultConfig {
                        minSdkVersion build_versions.minSdk
                        targetSdkVersion build_versions.compileSdk
                    }

                    compileOptions {
                        sourceCompatibility build_versions.java
                        targetCompatibility build_versions.java
                    }
                }

                androidExtensions {
                    experimental true
                }
            }
        }
    }
}
